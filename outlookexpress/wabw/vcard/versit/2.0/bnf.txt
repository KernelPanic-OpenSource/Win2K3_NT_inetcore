This syntax is written according to the form described in RFC 822,
but it references just this small subset of RFC 822 literals:
  CR          =  <ASCII CR, carriage return>  ; (     15,      13.)
  LF          =  <ASCII LF, linefeed>         ; (     12,      10.)
  CRLF	     =  CR LF
  SPACE       =  <ASCII SP, space>            ; (     40,      32.)
  HTAB        =  <ASCII HT, horizontal-tab>   ; (     11,       9.)

All literal property names are valid as upper, lower, or mixed case.

vcard_file	= [wsls] vcard [wsls]

vcard		= [ws] "BEGIN" [ws] ":" [ws] "VCARD" [ws 7bit] 1*CRLF
		items [ws] *CRLF
		[ws] "END" [ws] ":" [ws] "VCARD" [ws]
	; the optional 7bit indicates the version

ws		= 1*(SPACE / HTAB)
	; "whitespace," one or more spaces or tabs

wsls		= 1*(SPACE / HTAB / CRLF)
	; whitespace with line separators

items		= items *CRLF item
		/ item

item		= [ws] [groups "."] name
		  [params] ":" value CRLF
		/ [ws] [groups "."] "ADR"
		  [params] ":" addressparts CRLF
		/ [ws] [groups "."] "ORG"
		  [params] ":" orgparts CRLF
		/ [ws] [groups "."] "N"
		  [params] ":" nameparts CRLF
		/ [ws] [groups "."] "AGENT"
		  [params] ":" vcard CRLF

value		= 7bit / quoted-printable / base64

7bit		= <7bit us-ascii printable chars, excluding CR LF>

quoted-printable = <MIME RFC 1521 quoted-printable text>

base64		= <MIME RFC 1521 base64 text>
	; the end of the text is marked with two CRLF sequences
	; this results in one blank line before the start of the next property

groups		= groups "." word
		/ word

word		= <any printable 7bit us-ascii except []=:., >

params		= ";" [ws] paramlist

paramlist	= paramlist [ws] ";" [ws] param
		/ param

param		= "TYPE" [ws] "=" [ws] ptypeval
		/ "VALUE" [ws] "=" [ws] pvalueval
		/ "ENCODING" [ws] "=" [ws] pencodingval
		/ "CHARSET" [ws] "=" [ws] charsetval
		/ "LANGUAGE" [ws] "=" [ws] langval
		/ "X-" word [ws] "=" [ws] word
		/ knowntype

ptypeval	= knowntype / "X-" word

pvalueval	= "INLINE" / "URL" / "CONTENT-ID" / "X-" word

pencodingval 	= "7BIT" / "QUOTED-PRINTABLE" / "BASE64" / "X-" word

charsetval	= <a character set string as defined in Section 7.1 of RFC 1521>

langval		= <a language string as defined in RFC 1766>

addressparts	= 0*6(strnosemi ";") strnosemi
	; PO Box, Extended Addr, Street, Locality, Region, Postal Code, Country Name

orgparts	= *(strnosemi ";") strnosemi
	; First is Organization Name, remainder are Organization Units.

nameparts	= 0*4(strnosemi ";") strnosemi
	; Family, Given, Middle, Prefix, Suffix.
	; Example: Public;John;Q.;Reverend Dr.;III, Esq.

strnosemi	= *(*nonsemi ("\;" / "\" CRLF)) *nonsemi
	; To include a semicolon in this string, it must be escaped
	; with a "\" character.

nonsemi		= <any non-control ASCII except ";">

name		= "LOGO" / "PHOTO" / "LABEL" / "FN" / "TITLE"
		/ "SOUND" / "LANG" / "TEL" / "EMAIL" / "TZ" / "GEO" / "NOTE"
		/ "URL" / "BDAY" / "ROLE" / "REV" / "UID" / "KEY"
		/ "MAILER" / "X-" word 

knowntype	= "DOM" / "INTL" / "POSTAL" / "PARCEL" / "HOME" / "WORK"
		/ "PREF" / "VOICE" / "FAX" / "MSG" / "CELL" / "PAGER"
		/ "BBS" / "MODEM" / "CAR" / "ISDN" / "VIDEO"
		/ "AOL" / "APPLELINK" / "ATTMAIL" / "CIS" / "EWORLD"
		/ "INTERNET" / "IBMMAIL" / "MSN" / "MCIMAIL"
		/ "POWERSHARE" / "PRODIGY" / "TLX" / "X400"
		/ "GIF" / "CGM" / "WMF" / "BMP" / "MET" / "PMB" / "DIB"
		/ "PICT" / "TIFF" / "ACROBAT" / "PS" / "JPEG" / "QTIME"
		/ "MPEG" / "MPEG2" / "AVI"
		/ "WAVE" / "AIFF" / "PCM"
		/ "X509" / "PGP"
